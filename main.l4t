ffi "C" calloc
ffi "C" free
ffi "C" printf

struct Node
  num is_head
  num val
  ptr<struct Node> next

struct LinkedList
  ptr<struct Node> head
  ptr<struct Node> tail

func init_ll(ptr<struct LinkedList> ll) -> num
  ptr<struct Node> node
  node: calloc(3, 8)
  (*ll).head: node
  (*ll).tail: node
  return 0

func free_ll(ptr<struct LinkedList> ll) -> num
  ptr<struct Node> next, tmp
  next: (*ll).head
  loop next
    tmp: (*next).next
    free(next)
    next: tmp
  return 0

func push_ll(ptr<struct LinkedList> ll, num v) -> num
  ptr<struct Node> node
  node: calloc(3, 8)
  (*node).val: v
  (*(*ll).tail).next: node
  (*ll).tail: node
  return 0

func print_ll(ptr<struct LinkedList> ll) -> num
  ptr<struct Node> next
  next: (*(*ll).head).next
  loop next
    printf("%d\n", (*next).val)
    next: (*next).next
  return 0

func main() -> num
  struct LinkedList ll
  init_ll(&ll)
  push_ll(&ll, 10)
  push_ll(&ll, 200)
  push_ll(&ll, 0 - 3000)

  print_ll(&ll)
  free_ll(&ll)
  return 0
